if (IS_APPLICATION_RUN.toBoolean()){
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        if (IS_APPLICATION_RUN.toBoolean()){
            applicationId "com.xyoye.download_component"
        }
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        kapt {
            arguments {
                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }
        ndk {
            abiFilters 'armeabi-v7a'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug.initWith(release)

        beta.initWith(release)
    }

    sourceSets {
        main {
            if (IS_APPLICATION_RUN.toBoolean()){
                manifest.srcFile 'src/main/app_manifest/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }

    compileOptions{
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        dataBinding = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation rootProject.ext.dependencies["junit"]
    androidTestImplementation rootProject.ext.dependencies["test_junit"]
    androidTestImplementation rootProject.ext.dependencies["test_espresso-core"]

    implementation project(path: ':common_component')
    implementation project(path: ':repository:thunder')

    implementation files('libs/jlibtorrent-1.2.11.0.jar')
    implementation files('libs/jlibtorrent-android-arm-1.2.11.0.jar')

    kapt rootProject.ext.dependencies["arouter-compiler"]
}
